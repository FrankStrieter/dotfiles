#!/usr/bin/env zsh

set -eu
setopt pipe_fail extended_glob

directory=$TMP
if [[ -z $directory ]]; then
  printf 'Expected $TMP variable to be set\n' >&2
  exit 1
fi

exclude=
if [[ -n $SSH_AUTH_SOCK ]]; then
  exclude="~$SSH_AUTH_SOCK:A:h"
fi

# Glob explanation:
#   $directory/* = search below $directory
#   $exclude     = exclude items matching $exclude
#   N            = ignore no matches
#   D            = include files starting with a dot
#   u[$USER]     = include files owned by $USER
#   ^t           = exclude entries with the sticky bit (e.g. /tmp/uscreens)
pattern=$directory/*$exclude(NDu[$USER]^t)

# $~variable means zsh should expand the variable.
# http://unix.stackexchange.com/a/80575/72946
rm --recursive --force -- $~pattern
