#compdef git-rec
#description run command in git repositories
# shellcheck disable=SC2148

local context curcontext="$curcontext" state line
typeset -A opt_args
local ret=1

_arguments -C \
  '(- :)'{-h,--help}'[display help]' \
  '(-r --root)'{-r,--root}'[specify root directory]:root directory:_directories' \
  '(-n --no-git)'{-n,--no-git}'[do not prefix "git" to command]' \
  '*:: :->command' \
&& ret=0

case $state in
  (command)
    # If the first non-parsed word from above starts with a dash, assume
    # it will be completed as one of the options above.
    if [[ ${words[1]} == -* ]]; then
      return
    fi

    if (( ${+opt_args[-n]} || ${+opt_args[--no-git]} )); then
      :
    else
      # Prepend "git" to the words the _normal completer sees.
      words=($service "${words[@]}")
      (( CURRENT++ ))
    fi

    _normal \
      && ret=0
    ;;
esac

return ret

# vim: set ft=zsh ts=2 sw=2 et:
