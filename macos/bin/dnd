#!/usr/bin/env zsh

autoload -U colors && colors

toggle_dnd='tell application "BetterTouchTool"
  trigger_action "{\"BTTPredefinedActionType\":200}"
end tell
'

function dnd::on() {
  osascript <<< $toggle_dnd > /dev/null

  printf 'DND turned %bon%b\n' "$fg[green]" "$reset_color"
}

function dnd::off() {
  osascript <<< $toggle_dnd > /dev/null

  printf 'DND turned %boff%b\n' "$fg[red]" "$reset_color"
}

(($+commands[m])) || {
  >&2 printf 'Need m executable from https://github.com/rgcr/m-cli\n'
  exit 1
}

# Pass any argument to turn off DND.
if [[ $# -gt 0 ]]; then
  killall caffeinate 2> /dev/null
  m volume unmute
  open -a Flux
  osascript <<< 'tell application "PopClip" to set enabled to true'
  dnd::off
else
  # http://unix.stackexchange.com/a/148698/72946
  # http://stackoverflow.com/a/10408906/149264
  nohup caffeinate -u -t $((9 * 60 * 60)) < /dev/null > /dev/null 2>&1 &disown

  if ! scutil --dns | grep --quiet --fixed-strings home.therightstuff.de; then
    # Only mute while not at home.
    m volume mute
  fi

  killall Flux 2> /dev/null
  osascript <<< 'tell application "PopClip" to set enabled to false'
  dnd::on
fi
