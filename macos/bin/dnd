#!/usr/bin/env zsh

function dnd::enabled() {
  plutil \
    -convert xml1 \
    -o - \
    ~/Library/Preferences/ByHost/com.apple.notificationcenterui.*.plist | \
    grep --quiet doNotDisturbDate
}

function dnd::toggle() {
  # http://apple.stackexchange.com/a/244130
  osascript <<EOD
    tell application "System Events" to tell process "SystemUIServer"
      try
        key down option
        click menu bar item 1 of menu bar 1
        key up option
      on error errStr number errorNumber
        key up option
        error errStr number errorNumber
      end try
    end tell
EOD

  if [[ $? != 0 ]]; then
    >&2 printf "You need to grant %s accessibility access in Preferences\n" "$TERM_PROGRAM"
    open /System/Library/PreferencePanes/Security.prefPane/
    exit 1
  fi
}

function dnd::on()  dnd::enabled || dnd::toggle && echo DND turned on
function dnd::off() dnd::enabled && dnd::toggle && echo DND turned off || \
                      echo DND does not seem to be enabled && return 1

(($+commands[m])) || {
  >&2 printf 'Need m executable from https://github.com/rgcr/m-cli\n'
  exit 1
}

if [[ $# -gt 0 ]]; then
  killall caffeinate 2> /dev/null
  m volume unmute
  open -a Flux
  dnd::off
else
  # http://unix.stackexchange.com/a/148698/72946
  # http://stackoverflow.com/a/10408906/149264
  nohup caffeinate -u -t $((9 * 60 * 60)) < /dev/null > /dev/null 2>&1 &disown
  m volume mute
  killall Flux 2> /dev/null
  dnd::on
fi
