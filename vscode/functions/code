# This function makes it possible to start Visual Studio Code with Cygwin (Unix)
# paths. On other platforms it just calls `code` and assumes the executable is
# in the $PATH.
#
# To specify the path to your Visual Studio Code binary on Cygwin, set the
# VSCODE environment variable.
#
# For Cygwin it is required to pass absolute paths after other options
# separating them with `--`. Relative paths may be specified without `--`. E.g.
#
# code relative/path
# code -- /absolute/path
# code -- relative/path/will/be/made/absolute

local args=($@)

# Translate paths to Windows.
if [[ "$OSTYPE" == 'cygwin' ]]; then
  if [[ -n "$VSCODE" ]]; then
    local custom_binary="$VSCODE"
  fi

  local minus_minus_seen

  for ((i = 1; i <= $#args; i++)); do
    # rapid-git passes filename arguments that we need to translate after --.
    # You should, too.
    if [[ $args[$i] == '--' ]]; then
      minus_minus_seen=yes
      continue
    fi

    [[ -z $minus_minus_seen ]] && continue

    args[$i]="$(cygpath --windows --absolute $args[$i])"
  done
fi

if [[ -z "$custom_binary" ]]; then
  command code $args
else
  $custom_binary $args
fi

# vim: set ft=zsh ts=2 sw=2 et:
