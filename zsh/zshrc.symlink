# Loaded for all shells.

verbose Loading $fg[green]zshrc$reset_color

function zplug_load() {
  ZPLUG_HOME=$HOME/.cache/zplug
  if [[ ! -d $ZPLUG_HOME ]]; then
    verbose Downloading $fg[cyan]zplug$reset_color to $fg[green]$ZPLUG_HOME$reset_color
    mkdir --parents "$ZPLUG_HOME"
    wget -O "$ZPLUG_HOME/zplug" git.io/zplug

    verbose Loading $fg[cyan]zplug$reset_color
    source "$ZPLUG_HOME/zplug"

    zplug update --self
  else
    verbose Loading $fg[cyan]zplug$reset_color
    source "$ZPLUG_HOME/zplug"
  fi
}

function zplug_install() {
  # Install plugins if there are plugins that have not been installed.
  if zplug check --verbose; then
    return
  fi

  printf "Install? [y/N]: "
  if read -q; then
    echo
    zplug install
  fi
}

function() {
  # Cache function results.
  build-cache

  zplug_load

  # Save extended_glob option for restore. We need to change it for zplug's of tags.
  local eg=$options[extended_glob]
  setopt extended_glob

  local nice=
  # Load paths first.
  nice=-20; zplug "$DOTFILES", from:local, nice:$nice, of:"**/path.zsh"

  # Load everything but the zprofile, path, completion and postinit files second.
  nice=-19; zplug "$DOTFILES", from:local, nice:$nice, of:"**/(^(zprofile|path|completion|postinit)).zsh"

  # Set theme.
  nice=-18; zplug "$DOTFILES/zsh/themes", from:local, nice:$nice, of:"agross.zsh-theme"

  # Load postinit last.
  nice=9;   zplug "$DOTFILES", from:local, nice:$nice, of:"**/postinit.zsh"

  # Load completions after compinit was run.
  nice=10;  zplug "$DOTFILES", from:local, nice:$nice, of:"**/completion.zsh"

  zplug "b4b4r07/zplug"
  zplug "plugins/ssh-agent", from:oh-my-zsh, ignore:oh-my-zsh.sh
  zplug "bobthecow/git-flow-completion"
  zplug "rhysd/zsh-bundle-exec", if:"(($+commands[bundle])) && [[ ! "$OS" =~ ^Windows ]]"

  zplug "holman/dotfiles", as:command, of:"bin/e"
  zplug "agross/rapid-git", if:"(($+commands[git]))", of:"rapid-git.sh"

  zplug "zsh-users/zsh-syntax-highlighting", nice:10, if:'[[ ! "$OS" =~ ^Windows ]]'

  zplug_install

  # Source and add to the PATH.
  zplug load --verbose

  unfunction zplug_load zplug_install

  # Restore extended_glob option.
  options[extended_glob]=$eg

  startup-directory /scratch

  # Clean up after ourselves.
  undefine-startup-functions
}
