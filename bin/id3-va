#!/usr/bin/env zsh

set -eu
setopt pipe_fail

zmodload zsh/pcre
autoload -Uz colors && colors

exit_status=0

pcre_compile '^(.*) - (\d+) - (.*?) - (.*)\.mp3$'

for file in *.mp3(.); do
  printf '%s: ' $file

  if ! pcre_match -- $file; then
    printf '%bFile name does not match pattern%b\n' \
           $fg_bold[red] \
           $reset_color

    exit_status=1
    continue
  fi

  last_track=$((${track-0}))
  this_track=$(($match[2]))
  if ((last_track + 1 != this_track)); then
    printf '%bMissing track %s%b\n' \
            $fg_bold[red] \
            $((last_track + 1)) \
            $reset_color

    exit 2
  fi

  album=$match[1]
  track=$match[2]
  artist=$match[3]
  title=$match[4]

  printf '\nAlbum: %b%s%b | Track: %b%s%b | Artist: %b%s%b | Title: %b%s%b\n\n' \
          $fg_bold[green] \
          $album \
          $reset_color \
          $fg_bold[blue] \
          $track \
          $reset_color \
          $fg_bold[yellow] \
          $artist \
          $reset_color \
          $fg_bold[cyan] \
          $title \
          $reset_color

  id3 -1 -2 \
      --keep-time \
      --delete \
      --album $album \
      --track $track \
      --artist $artist \
      --title $title \
      $@ \
      -- $file

  id3 -- $file
done

exit $exit_status
