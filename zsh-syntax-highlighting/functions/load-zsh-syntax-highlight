# shellcheck disable=SC2148

# Get all styles with:
#
# print -l ${(ko)FAST_HIGHLIGHT_STYLES}
#
# and their color values with:
#
# Esc, X then enter fast-colors

local this_directory=${${(%):-%x}:h}

zle -N fast-colors

# Save compiled theme to temporary directory as I use different themes based
# on the iTerm profile.
FAST_WORK_DIR=$(mktemp --directory --suffix zsh-highlight-theme)

# https://github.com/zdharma/fast-syntax-highlighting/issues/203
touch $FAST_WORK_DIR/secondary_theme.zsh

typeset -Ua overlays
overlays=(
  8/overlay.ini
  $(echotc Co)/overlay.ini
)

if ((${+ITERM_PROFILE})); then
  overlays+=($ITERM_PROFILE/overlay.ini)
fi

local overlay
for overlay in $overlays; do
  overlay=$this_directory/../colors/$overlay

  if [[ -f $overlay ]]; then
    fast-theme $overlay > /dev/null
  fi
done

# vim: set ft=zsh ts=2 sw=2 et:
